ps.1.4

; r0 - base
; r1 - normal map
; r2 - normalization cube map
; r3 - normalization cube map

; t0 - base & normal & height
; t1 - normal
; t2 - L
; t3 - H
; t4 - eye

; c0 - ambient color
; c1 - diffuse color
; c2 - specular color

texld    r0, t0                 ; base
texld    r1, t0                 ; normal map
texld    r2, t2                 ; L
texld    r3, t3                 ; H

dp3_sat  r3, r1_bx2, r3_bx2     ; N.H

dp3_sat  r1.rgb, r1_bx2, r2_bx2 ; N.L
+mul     r2.a, r3.a, r3.a       ; N.H^2

mad      r1.rgb, c1, r1, c0     ; D*N.L + Amb
+mul     r2.a, r2.a, r2.a       ; N.H^4

mul      r0.rgb, r1, r0         ; (D*N.L + Amb)*Base
+mul     r2.a, r2.a, r2.a       ; N.H^8

mul     r2.a, r2.a, r2.a        ; N.H^16
mul     r2.a, r2.a, r2.a        ; N.H^32

mad     r0.rgb, c2, r2.a, r0    ; Spec*N.H^32 + (D*N.L + Amb)*Base
