ps.1.1
; c0 - ambient color
; c1 - diffuse color
; c2 - specular color
def c3, 1, 1, 1, 1

tex       t0             ; base
tex       t1             ; normal map
; v0                     ; L
tex       t2             ; Cubic Normalized H vector
texcoord  t3             ; Object Space Light Direction. Unit length is the light's range.

dp3_sat  r0, t1_bx2, t2_bx2     ; N.H

dp3_sat  r1.rgb, t1_bx2, v0_bx2 ; N.L
+mul     r0.a, r0.a, r0.a       ; N.H^2

mul      r0.rgb, c1, r1         ; D*N.L
+mul     r0.a, r0.a, r0.a       ; N.H^4

mul      r0.rgb, r0, t0         ; D*N.L*Base
+mul     r0.a, r0.a, r0.a       ; N.H^8

dp3_sat  r1.rgb, t3_bx2, t3_bx2 ; (Light Space Distance)^2 
+mul     r0.a, r0.a, r0.a       ; N.H^16

mad      r0.rgb, c2, r0.a, r0   ; S*N.H^16 + D*N.L*Base
+sub     r1.a, c3, r1.b         ; 1 - (Light Space Distance)^2

mul      r1.rgb, t0, c0         ; A*Base
+mul     r1.a, r1.a, r1.a       ; (1 - (Light Space Distance)^2)^2

mad      r0.rgb, r1.a, r0, r1   ; att*(D*N.L*Base + S*N.H^16) + A*Base


