ps.2.0
//		c0	Matrial ambient	
//		c1	Matrial diffuse	
//		c2	Matrial specular	
//		c3	Matrial power	
//		c4	Light ambient
//		c5	Light diffuse
//		c6	Light specular
def     c7,	0, 0.4, 1, 0
def		c8,	1, 0,   0, 0
dcl		t0	; tex
dcl		t1	; normal
dcl		t2	; eye
dcl		t3	; light
dcl_2d	s0

mov		r1,t1				
dp3		r0,r1,t3			;r0=dot(normal,light)

//주변광
mov		r1,c0				
mul		r1,r1,c4			;r1= m_a * l_a

//확산광
max		r2,r0,c7.xxxx		;r2=max( 0, dot( Normal, Light ))
mul		r2,r2,c1			;
mul		r2,r2,c5			;r2=확산광

//반사광
add		r4,r0,r0
mul		r4,r4,t1
add		r4,r4,-t3			;r4=라이트반사벡터
dp3		r6,r4,t2			;r5=dot(라이트반사벡터,eye)
max		r6.x,c7.xxxx,r6		;r6=max( 0, dot(반사벡터,eye))
pow		r5.x,r6.x,c3.x		;r5=r6^power
mul		r3,r5.xxxx,c2		
mul		r3,r3,c6			;r3=SpecularColor

// 최종색
add		r4,r1,r2			;(AmbientColor + DiffuseColor)

;texld	r5,t0,s0			;텍스쳐컬러
mov		r5,c8				;상수 컬러(red)

mul		r4,r4,r5			; *Color
add		r4,r4,r3			; +Specular
mov		oC0,r4

