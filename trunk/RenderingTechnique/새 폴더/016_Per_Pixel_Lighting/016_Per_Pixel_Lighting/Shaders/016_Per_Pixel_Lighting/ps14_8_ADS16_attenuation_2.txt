ps.1.4
; c0 - ambient color
; c1 - diffuse color
; c2 - specular color
def c3, 1, 1, 1, 1

texld   r0, t0              ; base
texld   r1, t1              ; normal map
texcrd  r2.xyz, t2          ; L
texcrd  r3.xyz, t3          ; H
texcrd  r4.rgb, t4          ; Object Space Light Direction. Unit length is the light's range.

dp3_sat  r3, r1_bx2, r3         ; N.H

dp3_sat  r3.rgb, r1_bx2, r2     ; N.L
+mul     r3.a, r3.a, r3.a       ; N.H^2

mul      r3.rgb, r3, c1         ; D*N.L
+mul     r3.a, r3.a, r3.a       ; N.H^4

mul      r3.rgb, r3, r0         ; D*N.L*Base
+mul     r3.a, r3.a, r3.a       ; N.H^8

dp3_sat  r4.rgb, r4, r4         ; (Light Space Distance)^2 
+mul     r3.a, r3.a, r3.a       ; N.H^16

mad      r3.rgb, c2, r3.a, r3   ; S*N.H^16 + D*N.L*Base
+sub     r4.a, c3, r4.r         ; 1 - (Light Space Distance)^2

mul      r0.rgb, r0, c0         ; A*Base
+mul     r4.a, r4.a, r4.a       ; (1 - (Light Space Distance)^2)^2

mad      r0.rgb, r4.a, r3, r0   ; att*(D*N.L*Base + S*N.H^16) + A*Base

