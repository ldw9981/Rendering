vs.1.0              // version instruction

m4x4 oPos, v0, c0   // transform vertices by view/projection matrix

// v0 - pozicia vrchola
// v1 - normalovy vektor

// c0-c3 contains composite transform matrix
// c6   pozicia kamery v suradniciach objektu
// c7   -2,-2,-2,-2


// inline vec ReflectedRay( const vec &in, const vec &n)	// in from eye to vertex
// return (in - 2.f*n*DOT3( in, n));

add  r0, v0, -c6		// r0 = in = vertex - eye

// normalize vector in r0
dp3  r0.w, r0, r0		// r0.w = |r0|^2
rsq  r0.w, r0.w			// r0.w = 1/sqrt(r0.w) = 1/|r0|
mul  r0.xyz, r0, r0.w	// r0 - normalized in vector

// DOT3( in, n)
dp3  r1, r0, v1		// r0 = in, v1 = n

// n*DOT3( in, n)
mul  r1, r1, v1		// v1 = n

// in - 2.f*n*DOT3( in, n) = f = r1
mad  oT0.xyz, r1, c7, r0		// r1 = r1*c7+r0

